buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java'
    id 'eclipse'

    // https://imperceptiblethoughts.com/shadow/
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    
    id 'com.github.spotbugs' version '4.2.2'
}

sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_14

wrapper.gradleVersion = '6.5'

import org.gradle.internal.os.OperatingSystem

ext {
    mainClass = 'com.gideonsoftware.mist.MIST';
    swtVersion = "4.15"
    shadowJar.classifier = "fat"

    mistLibraryPath = file("${buildDir}/libs")
    shadowJarPath   = file("${mistLibraryPath}/" + shadowJar.archiveFileName.get())
    jdepsReportPath = file("${buildDir}/reports/jdeps.txt")
    javaRuntimePath = file("${buildDir}/java-runtime")
    bundlePath      = file("${buildDir}/bundle")

    javaHome = System.properties.'java.home'
                
    operatingSystem = OperatingSystem.current()
    if (operatingSystem.isWindows()) {
        swtOs = 'win64'
        appDist = 'win64'
        appInfoTxtPath = file("${bundlePath}/appinfo.txt")
        innoSetupCompilerPath = file("C:\\Program Files (x86)\\Inno Setup 6\\iscc.exe")
        innoSetupInstallPath = file("package/os/windows/mist.iss")
        installerPath = "${bundlePath}\\${appName}-${appVersion}-${appDist}-setup.exe"
    } else if (operatingSystem.isMacOsX()) {
        swtOs = 'macos'
        appDist = 'macos'
        installerPath = "${bundlePath}/${appName}-${appVersion}-${appDist}.dmg"
    } else if (operatingSystem.isLinux()) {
        swtOs = 'linux'
        appDist = 'linux'
    }
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes('Main-Class': mainClass,
                   'Implementation-Title': appName,
                   'Implementation-Version': appVersion,
                   'Implementation-Vendor': appPublisher,
                   'Multi-Release' : 'true') // See https://stackoverflow.com/questions/53049346/is-log4j2-compatible-with-java-11
    }
}

dependencies {
    // Logging
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.3'

    // Command line option processing
    // https://mvnrepository.com/artifact/net.sf.jopt-simple/jopt-simple
    implementation group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '4.5'

    // IMAP server access & Gmail server response parsing functions
    // https://mvnrepository.com/artifact/com.sun.mail/javax.mail
    implementation group: 'com.sun.mail', name: 'javax.mail', version: '1.6.2' 

    // Google (Gmail) authentication    
    implementation group: 'com.google.apis', name: 'google-api-services-oauth2', version: 'v2-rev20190313-1.30.1'
    implementation group: 'com.google.oauth-client', name:'google-oauth-client-java6', version: '1.30.6'
    implementation group: 'com.google.oauth-client', name:'google-oauth-client-jetty', version: '1.30.6'
    
    // Gmail API Client Library
    implementation group: 'com.google.apis', name: 'google-api-services-gmail', version: 'v1-rev20200504-1.30.9'
    
    // For HTTP requests (as in UpdateModel)
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'
    
    // For JSON parsing (as in UpdateModel) 
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0'
    
    // HTML email parsing
    // https://mvnrepository.com/artifact/net.htmlparser.jericho/jericho-html
    implementation group: 'net.htmlparser.jericho', name: 'jericho-html', version: '3.4'

    // TntConnect MS Access database
    // https://mvnrepository.com/artifact/net.sf.ucanaccess/ucanaccess
    implementation group: 'net.sf.ucanaccess', name: 'ucanaccess', version: '5.0.0'

    // Money type
    // https://mvnrepository.com/artifact/org.javamoney.moneta/moneta-core
    implementation group: 'org.javamoney.moneta', name: 'moneta-core', version: '1.4'

    // SWT/JFace UI; note that SWT is platform-specific and is stored locally    
    // https://mvnrepository.com/artifact/org.eclipse.platform/org.eclipse.jface
    implementation (group: 'org.eclipse.platform', name: 'org.eclipse.jface', version: '3.19.0') {
        exclude group:'org.eclipse.platform', module: 'org.eclipse.swt'
    }
    implementation files("lib/swt/${swtOs}/swt-${swtVersion}.jar")

    // https://mvnrepository.com/artifact/com.github.spotbugs/spotbugs-annotations
    implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.0.3'
    
    testImplementation group: 'junit', name: 'junit', version: '4.13'
}

eclipse {
    classpath {
        file {
            whenMerged {
                def swtlib = entries.find { it.path.contains "swt-${swtVersion}.jar" }
                swtlib.sourcePath = fileReference(file("lib/swt/${swtOs}/swt-${swtVersion}-sources.zip"))
            }
        }
    }
}

// Warning: Internal API constructor TaskReportContainer(Class<T>, Task) has been deprecated. This is scheduled to be removed in Gradle 6.0.
//tasks.withType(com.github.spotbugs.SpotBugsTask) {
//    reports {
//        xml.enabled = false
//        html.enabled = true
//    }
//}

//spotbugs {
//    ignoreFailures = true
//    effort = "max"
//    reportLevel = "low"
//}

// Pro tip: If this task is failing, it could be due to an old gradle daemon.
// Try shutting down that daemon and a new (working) one will be created. 
task jdepsReport() {
    dependsOn 'shadowJar'
    group 'packaging'
    description 'Determine the needed Java modules'
    onlyIf { !jdepsReportPath.exists() }
    
    doLast {
        // Create parent directories if needed
        if (jdepsReportPath.getParentFile() != null) {
            jdepsReportPath.getParentFile().mkdirs(); 
        }    
        
        def stdOut = new FileOutputStream(jdepsReportPath)
 
        // jdk.crypto.ec is not picked up by jdeps but is necessary for GmailServer connection
        stdOut << "jdk.crypto.ec,"

        def result
        try { 
            result = exec {
                workingDir "${buildDir}"
    
                // jdeps --multi-release 14 --ignore-missing-deps --print-module-deps -cp libs <fat MIST jar>
                executable = "${javaHome}/bin/jdeps"
                args = [
                    '--multi-release', '14',
                    '--ignore-missing-deps',
                    '--print-module-deps',
                    "-cp", "${mistLibraryPath}", 
                    shadowJarPath]
    
                standardOutput = stdOut
            }
        } catch (all) {
            // Clean up in the event of failure
            if (result != 0)
              delete jdepsReportPath
            throw all
        }
    }
}

task javaRuntime() {
    dependsOn 'jdepsReport'
    group 'packaging'
    description 'Create a custom Java Runtime'
    onlyIf { !javaRuntimePath.exists() }
    
    doLast {
        exec {
            workingDir "${buildDir}"
            
            def moduleList = jdepsReportPath.text.trim();  // Need trim() else error (EOL?)
            
            // jlink --output <runtime loc> --no-header-files --no-man-pages --compress=2 --strip-debug --add-modules <modules>
            executable = "${javaHome}/bin/jlink"
            args = [
                '--output', "${javaRuntimePath}",
                '--no-header-files',
                '--no-man-pages',
                '--compress=2',
                '--strip-debug',
                '--add-modules', moduleList]
        }
    }
}

task createBundle() {
    dependsOn 'javaRuntime'
    group 'packaging'
    description 'Build the installable bundle'
    inputs.file shadowJarPath 
    inputs.dir javaRuntimePath
    outputs.dir bundlePath

    doLast {
        exec {
            workingDir "${buildDir}"
            
            // Due to JDK BUG 8217793 (https://bugs.openjdk.java.net/browse/JDK-8217793), 
            // jpackager throws an exception claiming that "Modules are not allowed in srcfiles"
            // This is thrown in regard to jlink, so if we call jlink (to create a java runtime) 
            // explicitly first, we can work around the bug  (Last checked 2019-07-25)
            
            // jpackager may work without ^that^ now; see "options for creating runtime image" in jpackage              

            def sharedArgs = [
                '--main-jar', shadowJar.archiveFileName.get(),
                '--input', mistLibraryPath,
                '--dest', bundlePath,
                '--runtime-image', javaRuntimePath,
                '--app-version', appVersion,
                '--description', appLongname,
                '--vendor', appPublisher,
                '--copyright', appCopyright,
            ]

            if (operatingSystem.isWindows()) {
                // jpackager creates a ton of [unneeded?] DLLs in the resulting bundle
                // See https://bugs.openjdk.java.net/browse/JDK-8222504 (Last checked 2019-07-25)
                executable = "${javaHome}\\bin\\jpackage.exe"
                args = [
                    '-t', 'app-image',
                    '-n', appName,
                    *sharedArgs,
                    '--module-path', "${javaHome}\\jmods",
                    '--icon', file('package/icons/mist.ico')
                ]
                    
            } else if (operatingSystem.isMacOsX()) {
                executable = "${javaHome}/bin/jpackage"
                args = [
                    '-t', 'dmg',
                    '-n', appName,
                    *sharedArgs,
                    '--module-path', "${javaHome}/jmods",
                    '--java-options', '-XstartOnFirstThread',
                    '--icon', file('package/icons/mist.icns')
                ]

            } else if (operatingSystem.isLinux()) {
                executable = "${javaHome}/bin/jpackage"
                args = [
                    '-t', 'deb',
                    '-n', appName.toLowerCase(),
                    *sharedArgs,                    
                    '--module-path', "${javaHome}/jmods",
                    '--icon', file('package/icons/mist-180x180.png'),
                    '--linux-deb-maintainer', appDebMaintainer,
                    //'--linux-package-name', appName, // Error? 
                    '--linux-app-category', 'Utility',
                    '--linux-shortcut'
                ]
            }
        }
    }    
}

// Additional processing for completing Windows bundle with InnoSetup
if (operatingSystem.isWindows()) {

    task appInfoTxt() {
        group 'packaging'
        description 'Create the appInfo.txt file needed by InnoSetup'
        onlyIf { !appInfoTxtPath.exists() }
        
        doLast {
            // First create the appInfo.txt file with needed properties
            appInfoTxtPath.text = """; Auto-generated by MIST build script
#define AppId "${appId}"
#define AppName "${appName}"
#define AppComment "${appLongname}"
#define AppVersion "${appVersion}"
#define AppVersion4Dot "${appVersion4Dot}"
#define AppURL "${appUrl}"
#define AppCopyright "${appCopyright}"
#define AppPublisher "${appPublisher}"
#define AppExeName "${appName}.exe"
#define AppDist "${appDist}"
"""
        }
    }

    task innoSetup() {
        dependsOn 'appInfoTxt'
        group 'packaging'
        description 'Run InnoSetup to generate the Windows installer'
        inputs.dir "$bundlePath/$appName"
        inputs.file appInfoTxtPath
        inputs.file innoSetupInstallPath
        outputs.file installerPath
        
        doLast {
            exec {
                workingDir "${buildDir}"
                
                // <innoSetup compiler> <install file path>
                executable = "${innoSetupCompilerPath}"
                args = [
                    '/Qp',
                    "${innoSetupInstallPath}"]
            }
        }
    }

    createBundle.finalizedBy innoSetup
}
