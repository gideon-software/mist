/*
Note: Warnings about deprecated features have to do with SpotBugs plugin.
"Deprecated Gradle features were used in this build, making it incompatible with Gradle 6.0."  
"Internal API constructor TaskReportContainer(Class<T>, Task) has been deprecated. This is scheduled to be removed in Gradle 6.0."
According to https://github.com/spotbugs/spotbugs-gradle-plugin/issues/95#issuecomment-492158555, this will
be fixed in 2.0.0.
*/

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.7.1"
    }
}

plugins {
    id 'java'
    id 'eclipse'

    // https://imperceptiblethoughts.com/shadow/
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    
    id 'com.github.spotbugs' version '1.7.1'
}

sourceCompatibility = 11
targetCompatibility = 11

version = appVersion
wrapper.gradleVersion = '5.5.1'

import org.gradle.internal.os.OperatingSystem

ext {
    mainClass = 'com.gideonsoftware.mist.MIST';
    swtVersion = "4.12"
    shadowJar.classifier = "fat"

    mistLibraryPath = file("${buildDir}/libs")
    shadowJarPath   = file("${mistLibraryPath}/" + shadowJar.archiveFileName.get())
    jdepsReportPath = file("${buildDir}/reports/jdeps.txt")
    javaRuntimePath = file("${buildDir}/java-runtime")
    bundlePath      = file("${buildDir}/bundle")

    operatingSystem = OperatingSystem.current()
    if (operatingSystem.isWindows()) {
        swtOs = 'win64'
        appDist = 'win64'
        extraJdepsArguments = [] // ['--ignore-missing-deps'] (not needed?)
        extraBundlerArguments = []
        appInfoTxtPath = file("${bundlePath}/appinfo.txt")
        innoSetupCompilerPath = file("C:\\Program Files (x86)\\Inno Setup 6\\iscc.exe")
        innoSetupInstallPath = file("package/os/windows/mist.iss")
        installerPath = "$bundlePath\\$appName-$appVersion-$appDist-setup.exe"
    } else if (operatingSystem.isMacOsX()) {
        swtOs = 'macosx'
        installerPath = "" // TODO
        jpackagerPath = file("/opt/jpackager/jpackager")
        extraJdepsArguments = []
    } else if (operatingSystem.isLinux()) {
        swtOs = 'linux'
        jpackagerPath = file("/opt/jpackager/jpackager")
        extraJdepsArguments = []
    }
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes('Main-Class': mainClass,
                   'Implementation-Title': appName,
                   'Implementation-Version': appVersion,
                   'Implementation-Vendor': appPublisher,
                   'Multi-Release' : 'true') // See https://stackoverflow.com/questions/53049346/is-log4j2-compatible-with-java-11
    }
}

dependencies {
    // Logging
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.0'

    // Command line option processing
    // https://mvnrepository.com/artifact/net.sf.jopt-simple/jopt-simple
    implementation group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '4.5'

    // IMAP server access & Gmail server response parsing functions
    // https://mvnrepository.com/artifact/com.sun.mail/javax.mail
    implementation group: 'com.sun.mail', name: 'javax.mail', version: '1.6.2' 

    // Google (Gmail) authentication    
    implementation group: 'com.google.apis', name: 'google-api-services-oauth2', version: 'v2-rev20190313-1.30.1'
    implementation group: 'com.google.oauth-client', name:'google-oauth-client-java6', version: '1.23.0'
    implementation group: 'com.google.oauth-client', name:'google-oauth-client-jetty', version: '1.23.0'
    
    // Gmail API Client Library
    implementation group: 'com.google.apis', name: 'google-api-services-gmail', version: 'v1-rev20200504-1.30.9'
    
    // HTML email parsing
    // https://mvnrepository.com/artifact/net.htmlparser.jericho/jericho-html
    implementation group: 'net.htmlparser.jericho', name: 'jericho-html', version: '3.4'

    // TntConnect MS Access database
    // https://mvnrepository.com/artifact/net.sf.ucanaccess/ucanaccess
    implementation group: 'net.sf.ucanaccess', name: 'ucanaccess', version: '4.0.4'

    // Money type
    // https://mvnrepository.com/artifact/org.javamoney.moneta/moneta-core
    implementation group: 'org.javamoney.moneta', name: 'moneta-core', version: '1.3'

    // SWT/JFace UI; note that SWT is platform-specific and is stored locally    
    // https://mvnrepository.com/artifact/org.eclipse.platform/org.eclipse.jface
    implementation (group: 'org.eclipse.platform', name: 'org.eclipse.jface', version: '3.16.0') {
        exclude group:'org.eclipse.platform', module: 'org.eclipse.swt'
    }
    implementation files("lib/swt/${swtOs}/swt-${swtVersion}.jar")

    // https://mvnrepository.com/artifact/com.github.spotbugs/spotbugs-annotations
    implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '3.1.12'
    
    testImplementation group: 'junit', name: 'junit', version: '4.11'
}

eclipse {
    classpath {
        file {
            whenMerged {
                def swtlib = entries.find { it.path.contains "swt-${swtVersion}.jar" }
                swtlib.sourcePath = fileReference(file("lib/swt/${swtOs}/swt-${swtVersion}-sources.zip"))
            }
        }
    }
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugs {
    ignoreFailures = true
    effort = "max"
    reportLevel = "low"
}

// Pro tip: If this task is failing, it could be due to an old gradle daemon.
// Try shutting down that daemon and a new (working) one will be created. 
task jdepsReport() {
    dependsOn 'shadowJar'
    group 'packaging'
    description 'Determine the needed Java modules'
    onlyIf { !jdepsReportPath.exists() }
    
    doLast {
        // Create parent directories if needed
        if (jdepsReportPath.getParentFile() != null) {
            jdepsReportPath.getParentFile().mkdirs(); 
        }    
        
        def stdOut = new FileOutputStream(jdepsReportPath)
 
        // jdk.crypto.ec is not picked up by jdeps but is necessary for GmailServer connection
        stdOut << "jdk.crypto.ec,"

        def result
        try { 
            result = exec {
                workingDir "${buildDir}"
    
                // jdeps --multi-release 11 [--ignore-missing-deps] --print-module-deps -cp libs <fat MIST jar>
                executable = 'jdeps'
                args = [
                    '--multi-release', '11',
                    *extraJdepsArguments, // Needed on Windows but not on Linux (TODO: Mac?)
                    '--print-module-deps',
                    "-cp", "${mistLibraryPath}", 
                    shadowJarPath]
    
                standardOutput = stdOut
            }
        } catch (all) {
            // Clean up in the event of failure
            if (result != 0)
              delete jdepsReportPath
            throw all
        }
    }
}

task javaRuntime() {
    dependsOn 'jdepsReport'
    group 'packaging'
    description 'Create a custom Java Runtime'
    onlyIf { !javaRuntimePath.exists() }
    
    doLast {
        exec {
            workingDir "${buildDir}"
            
            def moduleList = jdepsReportPath.text.trim();  // Need trim() else error (EOL?)
            
            // jlink --output <runtime loc> --no-header-files --no-man-pages --compress=2 --strip-debug --add-modules <modules>
            executable = 'jlink'
            args = [
                '--output', "${javaRuntimePath}",
                '--no-header-files',
                '--no-man-pages',
                '--compress=2',
                '--strip-debug',
                '--add-modules', moduleList]
        }
    }
}

task createBundle() {
    dependsOn 'javaRuntime'
    group 'packaging'
    description 'Build the installable bundle'
    inputs.file shadowJarPath 
    inputs.dir javaRuntimePath
    outputs.dir bundlePath

    doLast {
        exec {
            workingDir "${buildDir}"
            
            // Need to use a backport of jpackager for JDK 11
            // See https://mail.openjdk.java.net/pipermail/openjfx-dev/2018-September/022500.html?source=post_page---------------------------
            // For Windows:
            // - Move jpackager.exe -> %JAVA_HOME%\bin
            // - Move jdk.packager.jar -> %JAVA_HOME%\jmods            
            
            // Due to JDK BUG 8217793 (https://bugs.openjdk.java.net/browse/JDK-8217793), 
            // jpackager throws an exception claiming that "Modules are not allowed in srcfiles"
            // This is throw in regard to jlink, so if we call jlink (to create a java runtime) 
            // explicitly first, we can work around the bug  (Last checked 2019-07-25)            

            def sharedArgs = [
                //'--verbose',
                //'--echo-mode',
                '--main-jar', shadowJar.archiveFileName.get(),
                '--version', version,
                '--input', mistLibraryPath,
                '--output', bundlePath,
                '--runtime-image', javaRuntimePath,
                '--copyright', appCopyright
            ]

            if (operatingSystem.isWindows()) {

                // jpackager creates a ton of [unneeded?] DLLs in the resulting bundle
                // See https://bugs.openjdk.java.net/browse/JDK-8222504 (Last checked 2019-07-25)
                def javaHome = System.properties.'java.home'
                executable = "$javaHome\\bin\\java.exe"
                args = [
                    '-Xmx512M',
                    '--module-path', "$javaHome\\jmods",
                    '--add-opens', 'jdk.jlink/jdk.tools.jlink.internal.packager=jdk.packager',
                    '-m', 'jdk.packager/jdk.packager.Main',
                    'create-image',
                    *sharedArgs,
                    '--name', appName,
                    '--icon', file('package/icons/mist.ico')
                ]
                    
            } else {
                // Mac & Linux
                executable = jpackagerPath
                def macLinuxSharedArgs = [
                    '--name', appName.toLowerCase(),
                ]
            
                if (operatingSystem.isMacOsX()) {
                    // TODO: Test
                    args = [
                        'create-installer', 'dmg',
                        *sharedArgs,
                        *macLinuxSharedArgs,
                        '--icon', file('package/icons/mist.icns'),
                        '--mac-sign'
                    ]
                } else if (operatingSystem.isLinux()) {
                    args = [
                        'create-installer', 'deb',
                        *sharedArgs,
                        *macLinuxSharedArgs,
                        '--icon', file('package/icons/mist-180x180.png'),
                        '--linux-deb-maintainer', appDebMaintainer
                    ]
                }
            }
        }
    }    
}

// Additional processing for completing Windows bundle with InnoSetup
if (operatingSystem.isWindows()) {

    task appInfoTxt() {
        group 'packaging'
        description 'Create the appInfo.txt file needed by InnoSetup'
        onlyIf { !appInfoTxtPath.exists() }
        
        doLast {
            // First create the appInfo.txt file with needed properties
            appInfoTxtPath.text = """; Auto-generated by MIST build script
#define AppId "${appId}"
#define AppName "${appName}"
#define AppComment "${appLongname}"
#define AppVersion "${appVersion}"
#define AppVersion4Dot "${appVersion4Dot}"
#define AppURL "${appUrl}"
#define AppCopyright "${appCopyright}"
#define AppPublisher "${appPublisher}"
#define AppExeName "${appName}.exe"
#define AppDist "${appDist}"
"""
        }
    }

    task innoSetup() {
        dependsOn 'appInfoTxt'
        group 'packaging'
        description 'Run InnoSetup to generate the Windows installer'
        inputs.dir "$bundlePath/$appName"
        inputs.file appInfoTxtPath
        inputs.file innoSetupInstallPath
        outputs.file installerPath
        
        doLast {
            exec {
                workingDir "${buildDir}"
                
                // <innoSetup compiler> <install file path>
                executable = "${innoSetupCompilerPath}"
                args = [
                    '/Qp',
                    "${innoSetupInstallPath}"]
            }
        }
    }

    createBundle.finalizedBy innoSetup
}
